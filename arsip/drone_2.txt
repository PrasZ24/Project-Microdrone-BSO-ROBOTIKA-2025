#include <MPU6050_light.h>
#include "ArduPID.h"
#include <pid_tuner.h>

pid_tuner tuner;
ArduPID roll_pid;
MPU6050 sensor_mpu(Wire);
double angleY = 0;
double rollOut = 0;
double setPoint = 0;
double kp_roll, ki_roll, kd_roll = 0;
double pwm_1, pwm_2, pwm_3, pwm_4 = 0;
int throttle = 0;

unsigned long lastSend = 0;
unsigned long interval = 200;  // kirim tiap 200ms

void setup() {
  Serial.begin(9600);
  Serial.println("test");

  tuner.begin();
  mpuSetup();
  pidSetup();
  motorSetup();
}

void loop() {
  bacaSensor();
  motorControl();
  kirim_terimaData();
  // motorTest();
}

void mpuSetup() {
  Wire.begin(7, 6);
  sensor_mpu.begin(1, 3);
  sensor_mpu.upsideDownMounting = true;
  sensor_mpu.setFilterGyroCoef(0.92);
  Wire.beginTransmission(0x68);
  Wire.write(0x1A);
  Wire.write(0x05);
  Wire.endTransmission();
  delay(1000);
  sensor_mpu.calcOffsets();
}

void motorSetup() {
  ledcSetup(1, 1500, 8);
  ledcSetup(2, 1500, 8);
  ledcSetup(3, 1500, 8);
  ledcSetup(4, 1500, 8);

  ledcAttachPin(1, 1);
  ledcAttachPin(2, 2);
  ledcAttachPin(3, 3);
  ledcAttachPin(4, 4);
}
void pidSetup() {
  roll_pid.begin(&angleY, &rollOut, &setPoint, kp_roll, ki_roll, kd_roll);
  roll_pid.setOutputLimits(-100, 100);
  roll_pid.setWindUpLimits(-16, 16);
  roll_pid.start();
}

void bacaSensor() {
  sensor_mpu.update();
  angleY = -sensor_mpu.getAngleY();
}

void motorControl() {
  roll_pid.compute();
  pwm_1 = constrain(throttle - rollOut, 0, 255);
  pwm_2 = throttle;
  pwm_3 = constrain(throttle + rollOut, 0, 255);
  pwm_4 = throttle;

  ledcWrite(1, pwm_1);  // Motor kanan (Right)
  ledcWrite(2, pwm_2);  // Motor kanan (Atas)
  ledcWrite(3, pwm_3);  // Motor kiri (Left)
  ledcWrite(4, pwm_4);  // Motor kanan (Bawah)
}

void motorTest() {
  ledcWrite(1, 200);  // Motor kanan (Right)
  ledcWrite(3, 200);  // Motor kiri (Left)
}

void kirim_terimaData() {
  if (millis() - lastSend > interval) {
    tuner.sendAngle_roll(angleY);
    tuner.sendPWM_1(pwm_1);
    tuner.sendPWM_3(pwm_3);
    tuner.sendOutRoll_p(roll_pid.P());
    tuner.sendOutRoll_i(roll_pid.I());
    tuner.sendOutRoll_d(roll_pid.D());
    
    throttle = tuner.getThrottle();
    kp_roll = tuner.getRollP();
    ki_roll = tuner.getRollI();
    kd_roll = tuner.getRollD();
    roll_pid.setCoefficients(kp_roll, ki_roll, kd_roll);
    Serial.println(kp_roll);
    lastSend = millis();
  }
}